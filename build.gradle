plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.7'
}

group = 'dev.seeruk.plugin'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        // Pulled in for CommandAPI
        name = "codemc"
        url = "https://repo.codemc.org/repository/maven-public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT")

    implementation "net.kyori:adventure-api:4.17.0"

    implementation "dev.jorel:commandapi-bukkit-shade-mojang-mapped:9.5.2"
    compileOnly "dev.jorel:commandapi-annotations:9.5.2"
    annotationProcessor "dev.jorel:commandapi-annotations:9.5.2"
}

java {
    toolchain.setLanguageVersion(JavaLanguageVersion.of(21))
}

tasks.shadowJar {
    dependencies {
        include dependency("dev.jorel:commandapi-bukkit-shade-mojang-mapped:9.5.2")
    }

    relocate("dev.jorel.commandapi", "dev.seeruk.plugin.utils.commandapi")
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
